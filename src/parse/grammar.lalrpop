use parse::{Term, ReplCommand};
use lalrpop_util::ParseError;

grammar();

pub ReplCommand : ReplCommand = {
    "" => ReplCommand::NoOp,
    ":h" => ReplCommand::Help,
    <Term> => ReplCommand::Eval(Box::new(<>)),
    ":t" <Term> => ReplCommand::TypeOf(Box::new(<>)),
};

pub Term: Term = {
    LamTerm,
    <x: Term> ":" <t: LamTerm> => {
        Term::Ann(Box::new(x), Box::new(t))
    },
};

AtomicArg: (String, Option<Box<Term>>) = {
    <name: Ident> => (name, None),
    "(" <name: Ident> ":" <ann: PiTerm> ")" => (name, Some(Box::new(ann))),
};

LamTerm: Term = {
    PiTerm,
    r"\\" <name: Ident> ":" <ann: PiTerm> "=>" <body: LamTerm> => {
        Term::Lam(vec![(name, Some(Box::new(ann)))], Box::new(body))
    },
    r"\\" <args: AtomicArg+> "=>" <body: LamTerm> => {
        Term::Lam(args, Box::new(body))
    },
};

PiTerm : Term = {
    AppTerm,
    // Naively we would want to write the following rules:
    //
    // ```lalrpop
    // "(" <name: Ident> ":" <ann: PiTerm> ")" "->" <body: LamTerm> => {
    //      Term::Pi(name, Box::new(ann), Box::new(body))
    //  },
    //  <ann: AppTerm> "->" <body: LamTerm> => {
    //      Term::Arrow(Box::new(ann), Box::new(body))
    //  },
    // ```
    //
    // Alas this causes an ambiguity with the `AtomicTerm` rule. Therefore we
    // have to hack this in with a dyncamic check:
    <binder: AtomicTerm> "->" <body: LamTerm> =>? {
        match binder {
            Term::Ann(name, ann) => match *name {
                Term::Var(name) => Ok(Term::Pi(name, ann, Box::new(body))),
                _ => Err(ParseError::User {
                    error: "identifier expected in pi type", // TODO: better error!
                }),
            },
            ann => Ok(Term::Arrow(Box::new(ann), Box::new(body)))
        }
    }
};

AppTerm: Term = {
    AtomicTerm,
    <f: AppTerm> <arg: AtomicTerm> => {
        Term::App(Box::new(f), Box::new(arg))
    },
};

AtomicTerm: Term = {
    "(" <Term> ")",
    "Type" => Term::Type,
    Ident => Term::Var(<>),
};

Ident: String = {
    r"[a-zA-Z][a-zA-Z0-9]*" => String::from(<>),
};
