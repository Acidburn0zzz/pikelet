use parse::Term;

grammar();

pub Term: Term = {
    AbsTerm,
    // Annotated terms
    <x: Term> ":" <t: AbsTerm> => {
        Term::Ann(Box::new(x), Box::new(t))
    },
};

AbsTerm : Term = {
    AppTerm,
    <b: LamBind> "=>" <body: AbsTerm> => Term::Lam(b.0, b.1, Box::new(body)),
    <b: PiBind> "->" <body: AbsTerm> => Term::Pi(b.0, b.1, Box::new(body)),
};

AppTerm: Term = {
    AtomicTerm,
    // Applications
    <f: AppTerm> <x: AtomicTerm> => {
        Term::App(Box::new(f), Box::new(x)).into()
    },
};

AtomicTerm: Term = {
    "(" <Term> ")",
    "Type" => Term::Type,
    Ident => Term::Var(<>).into(),
};

LamBind : (String, Option<Box<Term>>) = {
    r"\\" <x: Ident> => (x, None),
    r"\\" <x: Ident> ":" <t: AppTerm> => (x, Some(Box::new(t))),
};

PiBind : (Option<String>, Box<Term>) = {
    <t: AppTerm> => (None, Box::new(t)),
    "[" <x: Ident> ":" <t: AppTerm> "]" => (Some(x), Box::new(t)),
};

Ident: String = {
    r"[a-zA-Z][a-zA-Z0-9]*" => String::from(<>),
};
