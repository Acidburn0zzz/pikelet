use source::pos::{BytePos, Span};
use std::u32;

use syntax::concrete::{Declaration, Exposing, Module, Term, ReplCommand};
use syntax::parse::{LalrpopError, ParseError, Token};

grammar<'err, 'src>(errors: &'err mut Vec<ParseError>, src: &'src str);

extern {
    type Location = BytePos;
    type Error = ParseError;

    enum Token<&'src str> {
        // Data
        "identifier" => Token::Ident(<&'src str>),
        "doc comment" => Token::DocComment(<&'src str>),
        "REPL command" => Token::ReplCommand(<&'src str>),
        "decimal literal" => Token::DecLiteral(<u64>),

        // Keywords
        "as" => Token::As,
        "module" => Token::Module,
        "import" => Token::Import,
        "Type" => Token::Type,

        // Symbols
        "\\" => Token::BSlash,
        ":" => Token::Colon,
        "," => Token::Comma,
        ".." => Token::DotDot,
        "=" => Token::Equal,
        "->" => Token::LArrow,
        "=>" => Token::LFatArrow,
        ";" => Token::Semi,

        // Delimiters
        "(" => Token::LParen,
        ")" => Token::RParen,
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
    }
}

pub ReplCommand: ReplCommand = {
    => ReplCommand::NoOp,
    <term: Term> => ReplCommand::Eval(Box::new(term)),
    <lo: @L> <command: "REPL command"> <hi: @R> =>? match command {
        "?" | "h" | "help" => Ok(ReplCommand::Help),
        "q" | "quit" => Ok(ReplCommand::Quit),
        command => {
            let span = Span::new(lo, hi);
            let command = String::from(command);
            Err(LalrpopError::User { error: ParseError::UnknownReplCommand { span, command} })
        },
    },
    <lo: @L> <command: "REPL command"> <hi: @R> <term: Term> =>? match command {
        "t" | "type" => Ok(ReplCommand::TypeOf(Box::new(term))),
        command => {
            let span = Span::new(lo, hi);
            let command = String::from(command);
            Err(LalrpopError::User { error: ParseError::UnknownReplCommand { span, command} })
        },
    },
};

pub Module: Module = {
    "module" <name: SpannedIdent> ";" <declarations: Declaration*> => {
        Module { name, declarations }
    },
};

pub Declaration: Declaration = {
    <lo: @L> "import" <name: SpannedIdent> <rename: ("as" <SpannedIdent>)?>
        <exposing: Exposing?> <hi: @R> ";" =>
    {
        Declaration::Import { span: Span::new(lo, hi), name, rename, exposing }
    },
    <name: SpannedIdent> ":" <ann: Term> ";" => {
        Declaration::Claim { name, ann }
    },
    <name: SpannedIdent> <params: AtomicLamParam*> "=" <body: Term> ";" => {
        Declaration::Definition { name, params, body }
    },
    <lo: @L> <recovered: !> <hi: @R> ";" => {
        errors.push(super::errors::from_lalrpop(src, recovered.error));
        Declaration::Error(Span::new(lo, hi))
    }
};

Exposing : Exposing = {
    <lo: @L> "(" ".." ")" <hi: @R> => Exposing::All(Span::new(lo, hi)),
    <lo: @L> "(" <names: (<ExposedIdent> ",")*> <last: ExposedIdent> <hi: @R> => {
        let mut names = names;
        names.push(last);
        Exposing::Exact(Span::new(lo, hi), names)
    },
    <lo: @L> <recovered: !> <hi: @R> => {
        errors.push(super::errors::from_lalrpop(src, recovered.error));
        Exposing::Error(Span::new(lo, hi))
    },
};

ExposedIdent : ((Span, String), Option<(Span, String)>) = {
    <SpannedIdent> <("as" <SpannedIdent>)?>
};

pub Term: Term = {
    LamTerm,
    <x: LamTerm> ":" <t: Term> => {
        Term::Ann(Box::new(x), Box::new(t))
    },
};

LamTerm: Term = {
    PiTerm,
    <lo: @L> "\\" <name: SpannedIdent> ":" <ann: PiTerm> "=>" <body: LamTerm> => {
        Term::Lam(lo, vec![(vec![name], Some(Box::new(ann)))], Box::new(body))
    },
    <lo: @L> "\\" <params: AtomicLamParam+> "=>" <body: LamTerm> => {
        Term::Lam(lo, params, Box::new(body))
    },
};

PiTerm: Term = {
    AppTerm,
    // Naively we would want to write the following rules:
    //
    // ```lalrpop
    // "(" <names: Ident+> ":" <ann: PiTerm> ")" "->" <body: LamTerm> => {
    //      Term::Pi(names, Box::new(ann), Box::new(body))
    //  },
    //  <ann: AppTerm> "->" <body: LamTerm> => {
    //      Term::Arrow(Box::new(ann), Box::new(body))
    //  },
    // ```
    //
    // Alas this causes an ambiguity with the `AtomicTerm` rule. Therefore we
    // have to hack this in by reparsing the binder:
    <lo: @L> <binder: AppTerm> "->" <body: LamTerm> <hi: @R> =>? {
        reparse_pi_type_hack(Span::new(lo, hi), binder, body)
    },
    <lo: @L> "Type" <level: U32Literal> <hi: @R> => {
        Term::Universe(Span::new(lo, hi), Some(level as u32))
    },
};

AppTerm: Term = {
    AtomicTerm,
    <f: AppTerm> <arg: AtomicTerm> => {
        Term::App(Box::new(f), Box::new(arg))
    },
};

AtomicTerm: Term = {
    <lo: @L> "(" <term: Term> ")" <hi: @R> => Term::Parens(Span::new(lo, hi), Box::new(term)),
    <lo: @L> "Type" <hi: @R> => Term::Universe(Span::new(lo, hi), None),
    <lo: @L> <ident: Ident> <hi: @R> => Term::Var(Span::new(lo, hi), ident),
    <lo: @L> <recovered: !> <hi: @R> => {
        errors.push(super::errors::from_lalrpop(src, recovered.error));
        Term::Error(Span::new(lo, hi))
    }
};

AtomicLamParam: (Vec<(Span, String)>, Option<Box<Term>>) = {
    <name: SpannedIdent> => (vec![name], None),
    "(" <names: SpannedIdent+> ":" <ann: PiTerm> ")" => (names, Some(Box::new(ann))),
};

SpannedIdent: (Span, String) = {
    <lo: @L> <ident: Ident> <hi: @R> => (Span::new(lo, hi), ident),
};

Ident: String = {
    "identifier" => String::from(<>),
};

U32Literal : u32 = {
    <lo: @L> <value: "decimal literal"> <hi: @R> =>? {
        u32_literal(Span::new(lo, hi), value)
    },
};
